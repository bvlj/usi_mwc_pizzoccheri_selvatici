plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

android {
    compileSdkVersion 31

    sourceSets {
        main {
            manifest {
                srcFile 'java/ch/usi/inf/mwc/cusi/AndroidManifest.xml'
            }
            java {
                srcDir 'java'
                includes = [
                        'java/ch/usi/inf/mwc/cusi/**/*.java',
                        'java/ch/usi/inf/mwc/cusi/**/*.kt',
                ]
            }
            res {
                srcDirs = [
                        'res',
                ]
            }
        }
        test {
            java {
                srcDir 'javatests'
            }
        }
    }

    defaultConfig {
        minSdkVersion 27
        targetSdkVersion 31
        versionCode 1
        versionName '1.0.0'

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        'room.schemaLocation': "$projectDir/schemas".toString()
                ]
            }
        }
    }

    buildFeatures {
        buildConfig = false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
    }

    dependenciesInfo {
        includeInApk false
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    def activity_version = '1.4.0'
    def core_ktx_version = "1.7.0"
    def coroutines_version = '1.5.2'
    def fragment_version = '1.4.0'
    def lifecycle_version = '2.4.0'
    def material_version = '1.4.0'
    def nav_version = '2.3.5'
    def preferences_version = '1.1.1'
    def recyclerview_version = '1.2.1'
    def room_version = "2.3.0"
    def swipe_refresh_layout_version = "1.1.0"
    def work_version = "2.7.1"

    implementation "androidx.core:core-ktx:$core_ktx_version"
    implementation "androidx.activity:activity-ktx:$activity_version"
    implementation "androidx.fragment:fragment-ktx:$fragment_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation "androidx.recyclerview:recyclerview:$recyclerview_version"
    implementation "androidx.preference:preference-ktx:$preferences_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:$swipe_refresh_layout_version"
    implementation "androidx.work:work-runtime-ktx:$work_version"
    implementation "com.google.android.material:material:$material_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"


    kapt "androidx.room:room-compiler:$room_version"
}
